find_path(LIBSSH2_INCLUDE_DIR libssh2.h)
find_library(LIBSSH2_LIBRARY NAMES ssh2 libssh2)
if(LIBSSH2_INCLUDE_DIR)
  file(STRINGS "${LIBSSH2_INCLUDE_DIR}/libssh2.h" LIBSSH2_VERSION REGEX "^#define[\t ]+LIBSSH2_VERSION[\t ]+\"([^\"]+)\"")
  string(REGEX REPLACE "^.*\"([^\"]+)\"" "\\1" LIBSSH2_VERSION ${LIBSSH2_VERSION})
endif()

if(LIBSSH2_LIBRARY)
  set(LIBSSH2_LIBRARIES ${LIBSSH2_LIBRARY})
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
  LibSSH2
  REQUIRED_VARS LIBSSH2_LIBRARIES LIBSSH2_INCLUDE_DIR
  VERSION_VAR LIBSSH2_VERSION
)

mark_as_advanced(LIBSSH2_INCLUDE_DIR LIBSSH2_LIBRARIES)

if(LibSSH2_FOUND)
  add_library(LibSSH2::LibSSH2 UNKNOWN IMPORTED GLOBAL)
  set_target_properties(LibSSH2::LibSSH2 PROPERTIES
    IMPORTED_LOCATION "${LIBSSH2_LIBRARY}"
    INTERFACE_COMPILE_OPTIONS "${LIBSSH2_COMPILE_OPTIONS}"
    INTERFACE_INCLUDE_DIRECTORIES "${LIBSSH2_INCLUDE_DIR}"
  )

endif()


